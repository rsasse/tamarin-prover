theory DH
begin

section{* The naive Diffie-Hellman Protocol *}

builtins: diffie-hellman

// Protocol
rule Init_1:
  [ Fr(~ekI) ]
  -->
  [ Init_1( $I, $R, ~ekI )
  , Out( <$I, $R, 'g' ^ ~ekI> ) ]

rule Init_2:
    let Y = 'g'^~s
    in
    [ Init_1( $I, $R, ~ekI )
    , In( <$R, $I, Y> ) 
    ]
  --[ SessionKey($I,$R, Y ^ ~ekI) ]->
    []

rule Resp:
    let X = 'g'^~s
    in
    [ Fr(~ekR)
    , In( <$I, $R, X> ) 
    ]
  --[ SessionKey($I,$R, X ^ ~ekR) ]->
    [
      Out( <$R, $I, 'g' ^ ~ekR> ) 
    ]

// VIOLATED: finds two attacks - each shows half of MitM attack - one vs initiator, one vs responder
lemma Key_Secret:
  "All I R sessKey #i. 
     SessionKey(I,R,sessKey) @ #i 
     ==> not (Ex #j. K(sessKey) @ #j)"

// solve with "A" (all solutions) to see different possibilities
lemma executable:
  exists-trace
  "Ex I R sessKey #i #j. SessionKey(I,R,sessKey)@ #i & SessionKey(I,R,sessKey)@ #j & not (#i = #j)"

end
